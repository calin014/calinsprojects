<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" minWidth="955" minHeight="600" xmlns:component="ro.calin.component.*" xmlns:model="ro.calin.component.model.*">
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.ObjectUtil;
			
			import ro.calin.app.model.Config;
			import ro.calin.component.event.CategoryEvent;
			import ro.calin.component.event.MenuEvent;
			import ro.calin.component.model.CategoryViewerModel;
			import ro.calin.component.model.PictureViewerModel;
			import ro.calin.utils.SWFProfiler;
			import ro.calin.utils.XmlToObjectConverter;
			
			private static const WALLPAPERS:String = "WALLPAPERS"; 
			
			private var config:Config = null;

			protected function configService_resultHandler(event:ResultEvent):void
			{
				config = new Config();
				
				XmlToObjectConverter.convertToObject(XML(event.result), config);
				
				menu.model = config.menu;
				setPictureViewerRandomWallpaper();
			}

			protected function configService_faultHandler(event:FaultEvent):void
			{
				Alert.show("Connection error. Contact admin.");
				trace(ObjectUtil.toString(event.fault));				
			}

			protected function menu_itemClickHandler(event:MenuEvent):void
			{
				if(currentState == "menustate") {
					if(event.entry.label == "gallery") {
						currentState = "gallerystate";
					} else {
						currentState = "pagestate";
					}
				} 
			}

			protected function menu_itemHoverHandler(event:MenuEvent):void
			{
				if(currentState == "gallerystate") {
					var catname:String = event.entry.extra as String;
					catViewer.model =  config.categories[catname];
					
					catViewer.height = Math.min(this.height - menu.height, 
						catViewer.model.subcategories.length * 119);
					//why 271???
					catViewer.x = 271 + 220 * event.buttonIndex;
					
					//TODO: use a nice fade in/out
					catViewer.visible = true;
					
					//TODO: also be sure to hide it when the user rolls out the button/menubar(can be a little tricky)
					catViewer.addEventListener(MouseEvent.ROLL_OUT, function(event:MouseEvent):void{
						catViewer.visible = false;
					});
				}
			}
			
			protected function menu_logoClickHandler(event:MenuEvent):void
			{
				currentState = "menustate";
				catViewer.visible = false;
				setPictureViewerRandomWallpaper();
			}

			protected function catViewer_categItemClickHandler(event:CategoryEvent):void
			{
				var picset:String = event.subcategory.extra as String;
				viewer.model = config.picturesets[picset];
				catViewer.visible = false;
			}
			
			private function setPictureViewerRandomWallpaper():void {
				var walls:Array = (config.picturesets[WALLPAPERS] as PictureViewerModel).pictures;
				var rand:int = Math.floor(Math.random() * walls.length);
				wallpaperModel.pictures[0] = walls[rand];
				viewer.model = wallpaperModel;
			}


		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="menustate"/>
		<s:State name="pagestate"/>
		<s:State name="gallerystate"/>
	</s:states>
	
	<s:transitions>
		<s:Transition toState="*">
			<s:Move target="{menu}" duration="300"/>
		</s:Transition>
	</s:transitions>
	
	<fx:Declarations>
		<s:HTTPService id="configService" url="config.xml" result="configService_resultHandler(event)"
					   fault="configService_faultHandler(event)" resultFormat="e4x"/>
		
		<model:PictureViewerModel id="wallpaperModel">
			<model:pictures>
				<fx:Array>
				</fx:Array>
			</model:pictures>
		</model:PictureViewerModel>
	</fx:Declarations>
	<s:applicationComplete>
		<![CDATA[
			//TODO: remove this on production and check from time to time
			SWFProfiler.init(stage, this);
			configService.send();
		]]>
	</s:applicationComplete>
	
	<component:PictureViewer id="viewer" width="100%" height="100%" />
	
	<component:Menu id="menu" logoSource="logo.jpg" 
					itemClick="menu_itemClickHandler(event)" 
					logoClick="menu_logoClickHandler(event)"
					itemHover="menu_itemHoverHandler(event)"
					width="100%" buttonWidth="220" y.menustate="{this.height * .7}" 
					y.pagestate="0" y.gallerystate="{this.height - menu.height}" />
	
	<component:CategoryViewer id="catViewer" visible="false" bottom="{menu.height}" 
							  thumbnailWidth="220" scale="1.2" categItemClick="catViewer_categItemClickHandler(event)"/>
	
</s:Application>
