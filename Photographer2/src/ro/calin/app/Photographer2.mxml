<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" minWidth="955" minHeight="600" xmlns:component="ro.calin.component.*">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import ro.calin.app.model.Config;
			import ro.calin.component.event.MenuEvent;
			import ro.calin.component.model.CategoryViewerModel;
			import ro.calin.component.model.MenuEntryModel;
			import ro.calin.component.model.MenuModel;
			import ro.calin.component.model.SubcategoryModel;
			import ro.calin.component.skin.MenuSkin;
			import ro.calin.utils.SWFProfiler;
			import ro.calin.utils.XmlToObjectConverter;
			
			private var config:Config = null;
			private static var mappings:Object = {
				/**Object mapping*/
				config:Config,
				
				menumodel:MenuModel,
				entry:MenuEntryModel,
				
				category: CategoryViewerModel,
				subcategory: SubcategoryModel,
				
				/**Property mapping for attrs that do not match props*/
				url: "extra",
				
				catname: "extra"
			};

			protected function configService_resultHandler(event:ResultEvent):void
			{
				config = XmlToObjectConverter.convert(XML(event.result), mappings);
				menu.model = config.menu;
			}

			protected function configService_faultHandler(event:FaultEvent):void
			{
				Alert.show("Error getting configuration!!!");
			}


			protected function menu_itemClickHandler(event:MenuEvent):void
			{
				if(currentState == "menustate") {
					if(event.entry.label == "gallery") {
						currentState = "gallerystate";
					} else {
						currentState = "pagestate";
					}
				} else if(currentState == "gallerystate") {
					var catname:String = event.entry.extra as String;
					var found:Boolean = false;
					
					//TODO: optimize this lookup with a hashmap or smfn
					for (var i:int = 0; i < config.categories.length; i++) {
						var model:CategoryViewerModel = config.categories.getItemAt(i) as CategoryViewerModel;
						if(model.extra as String == catname) {
							found = true;
							catViewer.model = model;
							break;
						}
					}
					
					if(!found) {
						throw new Error("Bad category name.");
					}
					
					catViewer.height = Math.min(this.height - menu.height, 
						catViewer.model.subcategories.length * 119);
					catViewer.visible = true;
					
					catViewer.addEventListener(MouseEvent.ROLL_OUT, function(event:MouseEvent):void{
						catViewer.visible = false;
					});
				}
			}


			protected function menu_logoClickHandler(event:MenuEvent):void
			{
				currentState = "menustate";
				viewer.source = ["data/back2.jpg"];
			}

		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="menustate"/>
		<s:State name="pagestate"/>
		<s:State name="gallerystate"/>
	</s:states>
	
	<s:transitions>
		<s:Transition toState="*">
			<s:Move target="{menu}" duration="300"/>
		</s:Transition>
	</s:transitions>
	
	<fx:Declarations>
		<s:HTTPService id="configService" url="config.xml" result="configService_resultHandler(event)"
					   fault="configService_faultHandler(event)" resultFormat="e4x"/>
	</fx:Declarations>
	<s:applicationComplete>
		<![CDATA[
			SWFProfiler.init(stage, this);
			configService.send();
		]]>
	</s:applicationComplete>
	
	<component:PictureViewer id="viewer" skinClass="ro.calin.component.skin.PictureViewerSkin" width="100%" height="100%">
		<component:source>
			<fx:Array>
				<fx:String>data/back2.jpg</fx:String>
			</fx:Array>
		</component:source>
	</component:PictureViewer>
	
	<component:Menu id="menu"
					logoSource="logo.jpg" 
					itemClick="menu_itemClickHandler(event)" 
					logoClick="menu_logoClickHandler(event)"
					width="100%" buttonWidth="200" y.menustate="{this.height * .7}" 
					y.pagestate="0" y.gallerystate="{this.height - menu.height}"/>
	
	<component:CategoryViewer id="catViewer" visible="false" bottom="{menu.height}" 
							  thumbnailWidth="220" scale="1.2"/>
	
</s:Application>
